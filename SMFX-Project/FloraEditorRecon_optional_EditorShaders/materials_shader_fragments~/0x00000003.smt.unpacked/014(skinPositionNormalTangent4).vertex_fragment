vertexFragment skinPositionNormalTangent4
	input blendWeights normal tangent position blendIndices
	output normal tangent position

	declareCode
		extern uniform float4 SkinBones[192];

		void SkinPosNormalTangent
		(
		int            numWeights,
		int4           indices,
		float4         boneWeights,
		inout float4   position,
		inout float3   normal,
		inout float3   tangent
		)
		{
		float4 row[3];
		row[0] = mul(boneWeights.x, SkinBones[indices.x + 0]);
		row[1] = mul(boneWeights.x, SkinBones[indices.x + 1]);
		row[2] = mul(boneWeights.x, SkinBones[indices.x + 2]);
		// this gets flattened, unless numWeights is variable
		for (int i = 1; i < numWeights; i++)
		{
		row[0] += mul(boneWeights[i], SkinBones[indices[i] + 0]);
		row[1] += mul(boneWeights[i], SkinBones[indices[i] + 1]);
		row[2] += mul(boneWeights[i], SkinBones[indices[i] + 2]);
		}
		// the compiler strips unused elements
		float4 p = position;
		position.x = dot(p, row[0]);
		position.y = dot(p, row[1]);
		position.z = dot(p, row[2]);
		float3 n = normal;
		normal.x = dot(n, row[0].xyz);
		normal.y = dot(n, row[1].xyz);
		normal.z = dot(n, row[2].xyz);
		float3 t = tangent;
		tangent.x = dot(t, row[0].xyz);
		tangent.y = dot(t, row[1].xyz);
		tangent.z = dot(t, row[2].xyz);
		if (nonUniformScale)
		{
		// apply the inverse scale
		float3 invScale2 = 1 / (row[0].xyz * row[0].xyz + row[1].xyz * row[1].xyz + row[2].xyz * row[2].xyz);
		normal  *= invScale2;
		tangent *= invScale2;
		}
		}
		void SkinPosNormal(int numWeights, int4 indices, float4 weights, inout float4 position, inout float3 normal)
		{
		float3 dummy = 0;
		SkinPosNormalTangent(numWeights, indices, weights, position, normal, dummy);
		}
		void SkinPos(int numWeights, int4 indices, float4 weights, inout float4 position)
		{
		float3 dummy = 0;
		SkinPosNormalTangent(numWeights, indices, weights, position, dummy, dummy);
		}
	endCode

	code
		SkinPosNormalTangent(4, Current.indices, Current.weights, Current.position, Current.normal.xyz, Current.tangent.xyz);
		In.position = Current.position;
	endCode
end
